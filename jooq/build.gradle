buildscript {
    ext {
        springBootVersion = '2.0.6.RELEASE'
    }
    repositories {
        mavenCentral()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("nu.studer:gradle-jooq-plugin:3.0.2")
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'nu.studer.jooq'

group = 'com.ewellhealth'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencyManagement {
    // io.spring.dependency-management would effect 3.10.8 as default
    dependencies {
        dependency ('org.jooq:jooq:3.11.5')
        dependency ('org.jooq:jooq-codegen:3.11.5')
        dependency ('org.jooq:jooq-meta:3.11.5')
    }
}

dependencies {
    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('org.springframework.boot:spring-boot-starter-jooq')
    implementation('org.jooq:jooq')

    // For JDK 9 and above
    // implementation('com.sun.xml.bind:jaxb-impl:+')
    // implementation('javax.xml.bind:jaxb-api:+')
    runtimeOnly('mysql:mysql-connector-java')
    jooqRuntime('mysql:mysql-connector-java')
    testImplementation('org.springframework.boot:spring-boot-starter-test')
}

jooq {
    version = '3.11.5'
    edition = 'OSS'
    cloudhis(sourceSets.main) {
        jdbc {
            // datasource configuration
            driver = 'com.mysql.jdbc.Driver'
            url = 'jdbc:mysql://localhost:3306'
            user = 'root'
            password = ''
        }
        generator {
            //jOOQ library <= 3.10.x: gradle-jooq plugin 2.0.11
            //jOOQ library >= 3.11.x: gradle-jooq plugin 3.0.0 or higher
            // If plugin version 2.0.x
//            name = 'org.jooq.util.DefaultGenerator' // Plugin 2.0.11
            // else
            name = 'org.jooq.codegen.DefaultGenerator' // Plugin 3.0.2
            database {
//                name = 'org.jooq.util.mysql.MySQLDatabase'
                name = 'org.jooq.meta.mysql.MySQLDatabase'
                includes = '.*'
                excludes = ''
                inputSchema = 'jooq' // Database name
            }
            target {
                directory = 'src/main/java'
                packageName = 'com.ewellhealth.jooq.autogen'
                clean = true
            }
        }
    }
}
